{
  parserClass="com.ronformat.parser.RonParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Ron"
  psiImplClassSuffix="Impl"
  psiPackage="com.ronformat.psi"
  psiImplPackage="com.ronformat.psi.impl"

  elementTypeHolderClass="com.ronformat.psi.RonTypes"
  elementTypeClass="com.ronformat.psi.RonElementType"
  tokenTypeClass="com.ronformat.psi.RonTokenType"
}

// TODO Add #![enable(...)]
ronFile ::= file_attributes? item_

private item_ ::= (value|COMMENT|BLOCK_COMMENT|CRLF)

file_attributes ::= FILE_ATTRIBUTES_BEGIN array

value ::= (unit|struct|enum|tuple|array|dictionary|STRING_LITERAL|NUMBER)

unit ::= STRUCT_BEGIN STRUCT_END

// Actually a tuple or an enum
enum ::= (IDENTIFIER tuple?)

tuple ::= STRUCT_BEGIN value (SEPARATOR value)* SEPARATOR? STRUCT_END

struct ::= IDENTIFIER? STRUCT_BEGIN field (SEPARATOR field)* SEPARATOR? STRUCT_END

array ::= ARRAY_BEGIN (value SEPARATOR)* value? ARRAY_END

dictionary ::= DICTIONARY_BEGIN (dictionary_field SEPARATOR)* dictionary_field? DICTIONARY_END

dictionary_field ::= value ASSIGN value

field ::= IDENTIFIER ASSIGN value
