{
  parserClass="com.ronformat.parser.RonParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Ron"
  psiImplClassSuffix="Impl"
  psiPackage="com.ronformat.psi"
  psiImplPackage="com.ronformat.psi.impl"

  elementTypeHolderClass="com.ronformat.psi.RonTypes"
  elementTypeClass="com.ronformat.psi.RonElementType"
  tokenTypeClass="com.ronformat.psi.RonTokenType"

  psiImplUtilClass="com.ronformat.psi.impl.RonPsiImplUtil"
}

// TODO Add #![enable(...)]
ronFile ::= file_attributes? value

file_attributes ::= FILE_ATTRIBUTES_BEGIN array

value ::= (unit|struct|enum|tuple|array|dictionary|literal)

literal ::= (STRING_LITERAL|NUMBER)

unit ::= STRUCT_BEGIN STRUCT_END

// Actually a tuple or an enum
enum ::= (IDENTIFIER tuple?)

tuple ::= STRUCT_BEGIN tuple_body STRUCT_END

tuple_body ::= value (SEPARATOR value)* SEPARATOR?

struct ::= IDENTIFIER? STRUCT_BEGIN struct_body SEPARATOR? STRUCT_END

struct_body ::= field (SEPARATOR field)*

field ::= IDENTIFIER ASSIGN value {methods=[getField]}

array ::= ARRAY_BEGIN array_body ARRAY_END

array_body ::= (value SEPARATOR)* value?

dictionary ::= DICTIONARY_BEGIN dictionary_body DICTIONARY_END

dictionary_body ::= (dictionary_field SEPARATOR)* dictionary_field?

dictionary_field ::= value ASSIGN value

comment ::= (END_OF_LINE_COMMENT|BLOCK_COMMENT)
